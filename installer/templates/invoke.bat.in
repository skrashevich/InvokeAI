@echo off

PUSHD "%~dp0"
setlocal

call .venv\Scripts\activate.bat
set INVOKEAI_ROOT=.

echo Do you want to generate images using the
echo 1. command-line
echo 2. browser-based UI
echo 3. run textual inversion training
echo 4. merge models (diffusers type only)
echo 5. re-run the configure script to download new models
echo 6. update InvokeAI
echo 7. open the developer console
echo 8. command-line help
set /P restore="Please enter 1, 2, 3, 4, 5, 6, 7 or 8: [2] "
if not defined restore set restore=2
IF /I "%restore%" == "1" (
    echo Starting the InvokeAI command-line..
    python .venv\Scripts\invokeai.exe %*
) ELSE IF /I "%restore%" == "2" (
    echo Starting the InvokeAI browser-based UI..
    python .venv\Scripts\invokeai.exe --web %*
) ELSE IF /I "%restore%" == "3" (
    echo Starting textual inversion training..
    python .venv\Scripts\invokeai-ti.exe --gui %*
) ELSE IF /I "%restore%" == "4" (
    echo Starting model merging script..
    python .venv\Scripts\invokeai-merge.exe --gui %*
) ELSE IF /I "%restore%" == "5" (
    echo Running invokeai-configure...
    python .venv\Scripts\invokeai-configure.exe %*
) ELSE IF /I "%restore%" == "6" (
    echo Running invokeai-update...
    python .venv\Scripts\invokeai-update.exe %*
) ELSE IF /I "%restore%" == "7" (
    echo Developer Console
    echo Python command is:
    where python
    echo Python version is:
    python --version
    echo *************************
    echo You are now in the system shell, with the local InvokeAI Python virtual environment activated,
    echo so that you can troubleshoot this InvokeAI installation as necessary.
    echo *************************
    echo *** Type `exit` to quit this shell and deactivate the Python virtual environment ***
    call cmd /k
) ELSE IF /I "%restore%" == "8" (
    echo Displaying command line help...
    python .venv\Scripts\invokeai.exe --help %*
    pause
    exit /b
) ELSE (
    echo Invalid selection
    pause
    exit /b
)
endlocal
